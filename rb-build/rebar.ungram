// Rebar Un-Grammar.
//
// This grammar specifies the structure of Rebar's concrete syntax tree.
// It does not specify parsing rules (ambiguities, precedence, etc are out of scope).
// Tokens are processed -- contextual keywords are recognised, compound operators glued.
//
// Legend:
//
//   //          -- comment
//   Name =      -- non-terminal definition
//   'ident'     -- token (terminal)
//   A B         -- sequence
//   A | B       -- alternation
//   A*          -- zero or more repetition
//   A?          -- zero or one repetition
//   (A)         -- same as A
//   label:A     -- suggested name for field of AST node

Name = 'ident'

BinaryOp = '+' | '-' | '*' | '/' | '%' | '==' | '!=' | '<' | '<=' | '>' | '>=' | '&&' | '||' | '&' | '|'
PrefixOp = '-' | '!'
PostfixOp = '-' // TODO

Literal = 'integer' | 'float' | 'true' | 'false' | 'nil'

Expr =
  Literal |
  Name |
  String |
  CallExpr |
  FieldExpr |
  PrefixExpr |
  PostfixExpr |
  BinaryExpr |
  IfExpr |
  Block

String = '"' 'char'* Escape* '"'
Escape = '\\' 'ident'

FieldExpr = Expr '.' Name
PrefixExpr = PrefixOp Expr
PostfixExpr = PostfixOp Expr
BinaryExpr = Expr BinaryOp Expr

IfExpr = 'if' Expr Block ('else' 'if' Expr Block)* ('else' Block)?

Block = '{' Stmt* '}'

Type = Name

CallExpr = Expr '(' ArgList ')'
ArgList = Expr (',' Expr)*

ExprStmt = Expr
Stmt = Let | Assign | Def | Struct | ExprStmt

Let = 'let' 'ident' (':' Type)? '=' Expr
Assign = Expr '=' Expr

Def = 'def' 'ident' Params Block
Struct = 'struct' Name StructBlock

Params = '(' Param* ')' ReturnType?
Param = 'ident' ':' Type
ReturnType = '->' Type

StructBlock = '{' StructItem* '}'
StructItem = Property | Def | ConstructorDef
ConstructorDef = 'constructor' Params Block

Property = Name ':' Type

SourceFile = Stmt*

// FIXME: Get rid of this hack. `nl` should be part of `SyntaxKind`, but not part of the grammar.
always_make_nl = 'nl' | '!'
